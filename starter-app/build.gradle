import com.proteusframework.build.GitInfo
import org.ajoberstar.grgit.Grgit

buildscript {
    ext.kotlin_version = '1.0.6'
    repositories {
        if(hasLocalRepo){ maven { url = local_repo } }
        maven {
            credentials {
                username "${repo_venturetech_username}"
                password "${repo_venturetech_password?:''}"
            }
            url repoReleaseURL
        }
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.3.0'
        // aspectj-weave plugin for our use of Spring @Configurable
        classpath 'com.i2rd:gradle-aspectj-weave:1.3',
            'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
    }

}
repositories {
//    mavenLocal()
    if(hasLocalRepo){ maven { url = local_repo } }
    maven {
        credentials {
            username repo_venturetech_username
            password repo_venturetech_password
        }
        url repoReleaseURL
    }
    maven {
        credentials {
            username repo_venturetech_username
            password repo_venturetech_password
        }
        url repoSnapshotURL
    }
    maven {
        url "https://dl.bintray.com/kotlin/exposed"
}   }
GitInfo.populateProject(project)

apply plugin: 'kotlin'
apply from: 'gradle/plugins.gradle'
apply from: 'gradle/config.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/artifacts.gradle'
apply from: 'gradle/tests.gradle'
apply from: 'gradle/aspectj.gradle'
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/analysis.gradle'
apply from: 'gradle/idea.gradle'

task createProject(type: com.proteusframework.build.CreateProjectTask)

task autoDeploy(type: com.proteusframework.build.DeployTask) {
    group = 'Publishing'
    id = "${findProperty('aws_id')?:'MISSING_ID'}"
    secret ="${findProperty('aws_secret')?:'MISSING_SECRET'}"
}


task wrapper(type: Wrapper) {
    group 'Build Setup'
    gradleVersion = '2.14'
}
task createDependencyFile() {
    group = 'Build'
    doFirst {
        def dependencyFile = file("dependencies.txt")
        def pw = dependencyFile.newPrintWriter()
        configurations.all.collect {it.resolvedConfiguration.getResolvedArtifacts()}.flatten().collect {it.moduleVersion.id}.unique().
            sort(false){"${it.group}:${it.name}"}forEach{
            pw.println(it)
            }
        pw.close()
        try{
            def root = file('../.git').exists() ? file('..') : file('.')
            def grgit = Grgit.open(root)
            def path = root == project.rootDir ? dependencyFile.name : "${project.rootDir.name}/${dependencyFile.name}"
            grgit.commit(message: 'Updating dependencies.txt', paths: [path] as Set)

        }catch(ignore){
            if(!ignore.message.toLowerCase().contains("no changes"))
                ignore.printStackTrace()
        }
    }
}
// IF this gets annoying, try `artifactoryPublish.doLast({createDependencyFile.execute()})`
compileJava.doLast({createDependencyFile.execute()})

//noinspection GroovyAssignabilityCheck
configurations.all {
    exclude group: 'org.eclipse.birt.runtime.3_7_1', module: 'org.apache.commons.codec'
    resolutionStrategy {
        dependencySubstitution {
            substitute module('bouncycastle:bcmail-jdk14:138') with module('org.bouncycastle:bcmail-jdk15on:1.52')
            substitute module('bouncycastle:bcprov-jdk14:138') with module('org.bouncycastle:bcprov-jdk15on:1.52')
            substitute module('bouncycastle:bctsp-jdk14:138') with module('org.bouncycastle:bctsp-jdk15on:1.46')
        }
        eachDependency {DependencyResolveDetails details ->
            if (details.requested.group == 'org.aspectj' && details.requested.name == 'aspectjweaver')
            {
                details.useVersion aspectjVersion
            }
            if (details.requested.group == 'xml-apis')
            {
                details.useVersion '1.4.+'
            }
        }
        // cache dynamic versions for ...
//        cacheDynamicVersionsFor 0, 'seconds'
        // cache changing modules (SNAPSHOT) for ...
        cacheChangingModulesFor 300, 'seconds'
    }
}


